#
# Exposes information about the Ruby environment via the $ruby_info associative
# array.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# function ruby-info {

local -a raw_output
local version
local gemset
local version_format
local version_formatted

# Clean up previous $ruby_info.
unset ruby_info
typeset -gA ruby_info

# Grab formatting for anything we might have to do
zstyle -s ':prezto:module:ruby:info:version' format 'version_format'

if [[ -n "$version_format" ]]; then
  if (( $+commands[rvm-prompt] )); then
    raw_output=($(rvm-prompt s v p g | tr '@' ' '))
    version=$raw_output[1]
    gemset=$raw_output[2]
  elif (( $+commands[rbenv] )); then
    version="$(rbenv version-name)"
  elif (( $+commands[ruby] )); then
    version="${${$(ruby --version)[(w)1,(w)2]}/ /-}"
  fi

  # Format version.
  if [[ -n "$version" && "$version" != "system" ]]; then
    zformat -f version_formatted "$version_format" "v:$version" "g:${gemset:=default}"
    ruby_info[version]="$version_formatted"
    ruby_info[gemset]="$gemset"
  fi
fi

# }
#  vim: set ts=8 sw=2 tw=0 ft=zsh et :
